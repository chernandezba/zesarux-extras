100 REMark  QUANTUM SOFT 1985
110 REMark    GUITAR CHORDS
115 MODE 8 : CSIZE #0,2,0 : CSIZE 2,0
120 WINDOW 512,255,0,0 : PAPER 0 : CLS : WINDOW 448,200,32,16 : WINDOW #0,448,39,32,216 : PAPER #0,0 : CLS : CLS #0
130 :
140 REMark opening menu
150 REPeat program
200   BORDER 1,2 : INK 4
205   FOR C = 70 TO 40 STEP -6
210     LINE 0,C TO 200,C
220     LINE C*4-137,40 TO C*4-137,70
250   END FOR C
260   CSIZE 3,1 : A$ = 'CHORD-IT'
270   INK 2 : OVER 1
275   FOR C = 5 TO 30 STEP 2
280     CURSOR 132+C,C : PRINT A$
290   END FOR C
295   INK 4
300   CURSOR 132+C,C : PRINT A$ : OVER 0 : INK 7
310   CSIZE 2,0 : AT 13,12 : PRINT 'PLEASE PRESS:-'
320   AT 15,9 : PRINT "'I' FOR INSTRUCTIONS" : AT 16,11 : PRINT "'C' TO CONTINUE"
330   INK 2 : CSIZE 2,0 : AT 18,10 : PRINT '1985 QUANTUM SOFT' : INK 7
335   REPeat read_keys
340     Key$ = INKEY$
342     IF Key$ =  CHR$(27) THEN EXIT program
345     IF Key$ == 'i' THEN INSTRUCT : CLS : NEXT program : REMark i for instructions, then redraw
350     IF Key$ == 'c' THEN EXIT read_keys : REMark C to continue
355   END REPeat read_keys
360   CONTROL
362 END REPeat program
365 :
367 STOP
370 :
600 DEFine PROCedure SCREEN
610   CLS : BORDER 1,2
620   CSIZE 2,0 : UNDER 1 : AT 1,7 : PRINT 'CHORD-IT'; : UNDER 0 : PRINT '   QUANTUM SOFT'
640   BEEP 300,20
650 END DEFine SCREEN
660 :
700 DEFine PROCedure MAIN
710   A$ = "A.A#,Bb.B.C.C#,Db.D.D#,Eb.E.F.F#,Gb.G.G#,Ab.Diminished.Augmented." : CSIZE 3,1 : UNDER 1 : CURSOR 155,25 : INK 2 : PRINT 'MAIN MENU' : CSIZE 2,0 : UNDER 0
720   AT 5,0 : W = 1 : OW = 1 : INK 4
725   FOR C = 1 TO LEN(A$)-14
730     IF A$(W+1) = '.' THEN 
740       PRINT ;'               ';A$(OW TO W)
750       OW = W + 2 : W = OW
760       NEXT C : EXIT C
770     END IF 
780     W = W + 1
790   END FOR C
795   PRINT #0,"  USE '¾' AND '¿' CURSOR TO SELECT"\"     PRESS 'ENTER' WHEN CHOSEN"\"       'F1' FOR INSTRUCTIONS"
800   INK 7 : X = 7 : AT X+4,14 : PRINT '½'
810   OX = X : kk$ = INKEY$
815   IF kk$ = CHR$(10)  THEN CLS #0 : RETurn 
820   IF kk$ = CHR$(232) THEN INSTRUCT : SCREEN : GO TO 710
840   IF kk$ = CHR$(208) THEN 
850     X = X - 1 : IF X < 1 THEN X = 14
860   END IF 
870   IF kk$ = CHR$(216) THEN 
880     X = X + 1 : IF X > 14 THEN X = 1
890   END IF 
900   IF OX = X THEN GO TO 810
910   AT OX+4,14 : PRINT ' ' : AT X+4,14 : PRINT '½' : BEEP 50,900 : GO TO 810
920 END DEFine MAIN
930 :
1000 DEFine PROCedure SECOND
1010   RESTORE 10000+((X-1)*100) : READ K$
1020   BEEP 300,20
1030   CLS : AT 1,21 : INK 2 : UNDER 1 : PRINT 'CHORD-IT' : UNDER 0 : AT 3,18 : PRINT ' QUANTUM SOFT' : AT 9,19 : INK 4 : PRINT 'KEY OF "';K$;'"' : INK 7
1040   A$=' .7.m.m7.6.m6.maj7.9.' : CSIZE 3,1 : AT 1,0
1050   OC = 1
1055   FOR C = 1 TO LEN(A$)
1060     IF A$(C) = '.' THEN 
1070       KEY : OC = C + 1 : C = OC : NEXT C : EXIT C
1080     END IF 
1100   END FOR C
1110   PRINT #0,"  USE '¾' AND '¿' CURSOR TO SELECT"\"     PRESS 'ENTER' WHEN CHOSEN"
1120   Y = 1 : AT Y,3 : INK 4 : PRINT '½'
1130   OY = Y : kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 1250
1140   IF kk$ = CHR$(208) THEN 
1150     Y = Y - 1 : IF Y < 1 THEN Y = 8 : GO TO 1200
1160   END IF 
1170   IF kk$ = CHR$(216) THEN 
1180     Y = Y + 1 : IF Y > 8 THEN Y = 1
1190   END IF 
1200   IF OY = Y THEN GO TO 1130
1210   AT OY,3 : PRINT ' ' : AT Y,3 : PRINT '½' : BEEP 70,20 : FOR C=1 TO 20 : END FOR C : GO TO 1130
1250   AT 0,0 : CSIZE 2,0
1290 END DEFine SECOND
1295 :
1300 DEFine PROCedure KEY
1310   PRINT '    ';K$;A$(OC TO C-1)
1320 END DEFine KEY
1330 :
1400 DEFine PROCedure DRAW
1420   SELect ON Y
1430     ON Y = 1 : T$ = ''
1440     ON Y = 2 : T$ = '7'
1450     ON Y = 3 : T$ = 'm'
1460     ON Y = 4 : T$ = 'm7'
1470     ON Y = 5 : T$ = '6'
1480     ON Y = 6 : T$ = 'm6'
1490     ON Y = 7 : T$ = 'maj7'
1500     ON Y = 8 : T$ = '9'
1510   END SELect 
1512   RESTORE 10000+((X-1)*100)+(Y+1)*10 : READ Q,R
1520   CLS : CLS #0 : INK 7 : CSIZE 3,1 : A$ = 'CHORD "'&K$&T$&'"' : DRAW2
1522 END DEFine DRAW
1528 :
1535 DEFine PROCedure FRET
1540   INK 4
1545   FOR C = 7 TO 140 STEP 55
1550     FOR V = 1 TO 48 STEP 8
1560       LINE C+V,28 TO C+V,70
1570       LINE C+1,V+28 TO C+41,V+28
1600     END FOR V
1605   END FOR C
1607   INK 7
1610 END DEFine FRET
1617 :
1625 DEFine PROCedure DRAW2
1630   AT 1,(27-LEN(A$))/2 : PRINT A$ : CSIZE 2,0 : FRET
1660   POINT 8,68 : LINE_R  TO 40,0 TO 0,2 TO -40,0 TO 0,-2
1670   IF Q <> 0 THEN 
1674     CHAR 2,6.5,.5,Q : GO TO 1690
1675   END IF 
1680   POINT 63,68 : LINE_R  TO 40,0 TO 0,2 TO -40,0 TO 0,-2
1690   IF R <> 0 THEN 
1694     CHAR 3,6.5+(.5*(LEN(R)>1)),.5,R : GO TO 1750
1695   END IF 
1700   POINT 118,68 : LINE_R  TO 40,0 TO 0,2 TO -40,0 TO 0,-2
1750   REMark GO TO 2000
2000   FOR B = 1 TO 3
2010     READ N$
2015     FOR D = 1 TO 6
2020       IF N$(D) = 'N'THEN 
2030         OVER 1 : CHAR B,7-D,0,'X' : OVER 0
2040       END IF 
2050     END FOR D
2055   END FOR B
2060   FOR E = 1 TO 3
2070     FOR G = 1 TO 4
2100       READ P$
2110       IF P$ = '00' THEN GO TO 2200
2120       IF P$(1) = 'Y' THEN 
2130         INK 2
2135         FOR F = P$(4) TO P$(3)
2140           CHAR E,F,P$(2),G
2150         END FOR F
2155         INK 7 : GO TO 2200
2160       END IF 
2170       CHAR E,P$(1),P$(2),G
2200     END FOR G
2210   END FOR E
2250 END DEFine DRAW2
2260 :
2300 DEFine PROCedure CHAR(P,J,K,F$)
2305 pp=P
2310   SELect ON pp
2330     ON pp = 1 : A = 96
2340     ON pp = 2 : A = 243
2350     ON pp = 3 : A = 390
2355     ON pp = REMAINDER : PRINT 'FRET BOARD ERROR!' : STOP
2360   END SELect 
2370   M = A + (48 - (21.6 * J))
2380   N = 65 + (15.5 * (K - 1))
2390   CURSOR M,N : PRINT F$
2400 END DEFine CHAR
2410 :
3000 DEFine PROCedure DIMINISH
3010   CLS : CLS#0 : CSIZE 3,1 : UNDER 1 : AT 1,5 : INK 7 : PRINT 'DIMINISHED CHORDS' : UNDER 0 : INK 4
3020   A$ = 'A-,C-,D#-/Eb-,F#-/Gb-.A#-/Bb-,C#-/Db-,E-,G-.B-,D-,F-,G#-/Ab-.' : DIM D$(3,21)
3030   AT 2,0 : V = 1 : I = 1
3035   FOR C = 1 TO LEN(A$)
3040     IF A$(C) = '.' THEN 
3050       PRINT \'    ';A$(V TO C-1) : D$(I) = A$(V TO C-1)
3060       V = C + 1 : I = I + 1
3070     END IF 
3080   END FOR C
3090   CSIZE 2,0 : AT 18,9 : INK 2 : PRINT ' QUANTUM SOFT 1985'
3100   PRINT #0,"  USE '¾' AND '¿' CURSOR TO SELECT"\"     PRESS 'ENTER' WHEN CHOSEN"
3110   AT 0,0 : CSIZE 3,1 : AT 5,3 : INK 7 : PRINT '½'
3120   S = 2 : OS = S
3130   kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 3300
3135   IF kk$ = CHR$(208) THEN 
3140     S = S - 1 : IF S < 1 THEN S = 3
3150   END IF 
3160   IF kk$ = CHR$(216) THEN 
3170     S = S + 1 : IF S > 3 THEN S = 1
3180   END IF 
3190   IF S = OS THEN GO TO 3130
3200   AT (OS*2)+1,3 : PRINT ' '
3210   AT (S*2)+1,3 : PRINT '½'
3220   OS = S : BEEP 70,20
3230   PAUSE 25 : REMark FOR C = 1 TO 50 : END FOR C
3240   GO TO 3130
3300   A$ = D$(S) : RESTORE 10000+((X-1)*100)+((S-1)*10) : CLS : CLS #0 : READ Q,R : DRAW2
3310   AT 0,0 : CSIZE 2,0 : AT 16,2 : PRINT "PRESS 'ENTER' FOR MORE DIMINISHED"
3320   AT 18,5 : PRINT "PRESS 'SPACE' FOR MAIN MENU" : AT 0,4 : INK 2 : PRINT 'QUANTUM SOFT 1985 P SPROSTON' : INK 7
3330   kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 3010
3340   IF kk$ = ' ' THEN RETurn 
3350   GO TO 3330
3360 END DEFine DIMINISH
3370 :
4000 DEFine PROCedure AUGMENT
4010   CLS : CLS #0 : CSIZE 3,1 : UNDER 1 : AT 0,6 : INK 7 : PRINT 'AUGMENTED CHORDS' : UNDER 0 : INK 4
4020   A$ = 'C+,E+,G#+/Ab+.F+,A+,C#+/Db+.D+,A#+/Bb+,F#+/Gb+.G+,B+,D#+/Eb+.' : DIM D$(4,21)
4030   AT 1,0 : V = 1 : I = 1
4035   FOR C = 1 TO LEN(A$)
4040     IF A$(C) = '.' THEN 
4050       PRINT \'       ';A$(V TO C-1) : D$(I) = A$(V TO C-1)
4060       V = C + 1 : I = I + 1
4070     END IF 
4080   END FOR C
4090   AT 0,0 : CSIZE 2,0 : AT 18,9 : INK 2 : PRINT ' QUANTUM SOFT 1985'
4100   PRINT #0,"  USE '¾' AND '¿' CURSOR TO SELECT"\"     PRESS 'ENTER' WHEN CHOSEN"
4110   AT 0,0 : CSIZE 3,1 : AT 2,6 : INK 7 : PRINT '½'
4120   S = 1 : OS = S
4130   kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 4300
4135   IF kk$ = CHR$(208) THEN 
4140     S = S - 1 : IF S < 1 THEN S = 4
4150   END IF 
4160   IF kk$ = CHR$(216) THEN 
4170     S = S + 1 : IF S > 4 THEN S = 1
4180   END IF 
4190   IF S = OS THEN GO TO 4130
4200   AT (OS*2),6 : PRINT ' '
4210   AT (S*2),6 : PRINT '½'
4220   OS = S : BEEP 70,20
4230   PAUSE 25 : REMark FOR C = 1 TO 50 : END FOR C
4240   GO TO 4130
4300   A$ = D$(S) : RESTORE 10000+((X-1)*100)+((S-1)*10) : CLS : CLS #0 : READ Q,R : DRAW2
4310   AT 0,0 : CSIZE 2,0 : AT 16,2 : PRINT "PRESS 'ENTER' FOR MORE AUGMENTED"
4320   AT 18,5 : PRINT "PRESS 'SPACE' FOR MAIN MENU" : AT 0,4 : INK 2 : PRINT 'QUANTUM SOFT 1985 P SPROSTON' : INK 7
4330   kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 4010
4340   IF kk$ = ' ' THEN RETurn 
4350   GO TO 4330
4360 END DEFine AUGMENT
4370 :
5000 DEFine PROCedure INSTRUCT
5010   CLS : CLS #0
5020   AT 0,0 : CSIZE 2,0 : AT 1,13 : INK 4 : UNDER 1 : PRINT 'CHORD-IT' : UNDER 0 : INK 7
5030   PRINT \\'THIS PROGRAM IS AN INFORMATION SYSTEMWHICH GIVES YOU THREE VERSIONS OF    ALMOST ANY CHORD.'\\\
5040   PRINT 'THE INFORMATION IS ACCESSED BY A VERYSIMPLE MENU SYSTEM WHERE YOU MOVE THEARROW PROVIDED, UP OR DOWN UNTIL IT  IS NEXT TO THE SECTION YOU REQUIRE,  THEN IT IS A SIMPLE CASE OF PRESSING THE "ENTER" BUTTON.'
5050   PRINT \\'THIS LEADS TO ANOTHER MENU WHERE THE PROCESS IS REPEATED.'
5060   PRINT #0,'      PRESS "ENTER" WHEN READY'
5070   IF CODE(INKEY$) <> 10 THEN GO TO 5070
5080   CLS #0 : AT 3,0
5090   FOR C = 1 TO 16 : PRINT '                                     '
5100   AT 3,0 : PRINT 'EACH FINGER IS GIVEN A NUMBER:-'
5110   PRINT '         LARGEST FINGER-1'
5120   PRINT '         SMALLEST FINGER-4'\\
5130   PRINT 'WHERE A FINGER COVERS MORE THAN ONE  STRING THE FINGER NUMBER IS IN '; : INK 2 : PRINT 'RED'; : INK 7 : PRINT '.'\\
5140   PRINT 'A CROSS("X") AT THE TOP OF A STRING  SHOWS THAT THE STRING SHOULD NOT BE  PLAYED.'\\
5150   PRINT 'A NUMBER AT THE TOP LEFT OF A CHORD  DIAGRAM SHOWS WHICH FRET THE CHORD   SHOULD BE PLAYED FROM.'
5160   PRINT \'OTHER FINGER POSITIONS ARE POSSIBLE.'
5170   PRINT #0,'      PRESS "ENTER" WHEN READY'
5180   IF CODE(INKEY$) <> 10 THEN GO TO 5180
5190   CLS #0
5195 END DEFine INSTRUCT
5205 :
9000 DEFine PROCedure CONTROL
9010   SCREEN
9020   MAIN
9030   IF X = 13 THEN DIMINISH : GO TO 9010
9040   IF X = 14 THEN AUGMENT  : GO TO 9010
9060   SECOND
9070   DRAW
9080   A$ = "PRESS 'ENTER' FOR KEY OF '"&K$&"'" : AT 16,(37-LEN(A$))/2 : PRINT A$
9090   AT 18,5 : PRINT "PRESS 'SPACE' FOR MAIN MENU" : AT 0,4 : INK 2 : PRINT 'QUANTUM SOFT 1985 P SPROSTON' : INK 7
9100   kk$ = INKEY$ : IF kk$ = CHR$(10) THEN GO TO 9060
9110   IF kk$ = ' ' THEN GO TO 9010
9120   GO TO 9100
9800 END DEFine CONTROL
9810 :
9994 STOP
9995 :
9996 DEFine PROCedure Sa
9997   DELETE MDV1_CHORD:SAVE MDV1_CHORD
9998 END DEFine Sa
9999 :
10000 DATA 'A'
10020 DATA 5,9,'YYYYYY','YYYYYY','YYYYYY','00','42','32','22','Y161','32','53','43','Y131','22','43','00'
10030 DATA 5,7,'YYYYYY','YYYYYY','YYYYYY','Y241','00','13','00','Y161','32','53','24','41','22','33','13'
10040 DATA 5,8,'YYYYYY','YYYYYY','YYYYYY','21','32','42','00','Y161','00','43','53','11','32','43','23'
10050 DATA 5,8,'YYYYYY','YYYYYY','YYNYYY','21','42','32','13','Y161','00','53','24','11','32','53','23'
10060 DATA 5,7,'YYYYYY','YYNYYY','YYYYYY','Y241','00','00','00','Y161','32','53','23','Y151','00','33','13'
10070 DATA 5,7,'YYYYYY','YYNYYY','YYYYYY','21','42','32','12','Y161','00','53','23','Y152','12','33','00'
10080 DATA 4,5,'YYYYYY','NYYYYY','YNYYYN','31','42','22','00','11','22','33','44','61','21','42','32'
10090 DATA 6,11,'YYYYYY','YYYYYY','YNYYYN','00','42','32','13','31','42','12','23','41','62','32','22'
10100 DATA '[A#/Bb]'
10120 DATA 0,6,'NNYYYY','NNYYYY','YYYYYY','11','43','33','23','Y341','00','00','16','Y161','32','53','43'
10130 DATA 0,6,'NNYYYY','NNYYYY','YYYYYY','Y131','43','23','00','Y341','00','14','00','Y161','32','53','24'
10140 DATA 6,9,'NNYYYY','YYYYYY','NNYYYY','11','22','43','33','Y161','00','53','43','11','32','43','23'
10150 DATA 6,9,'NNYYYY','YYYYYY','YYNYYY','22','43','33','14','Y161','00','53','24','11','32','53','23'
10160 DATA 6,8,'NNYYYY','YYNYYY','NYYYYY','Y341','00','00','00','Y161','32','53','23','Y152','00','33','13'
10170 DATA 6,8,'NNYYYY','YYNYYY','NYYYYY','22','43','33','13','Y161','53','00','23','Y152','12','33','00'
10180 DATA 0,5,'NNYYYY','YYYYYY','NNYYYY','Y341','00','00','15','Y161','32','43','23','11','22','33','44'
10190 DATA 7,8,'NNYYYY','NNYYYY','YYYYYY','21','43','33','14','31','42','12','23','Y161','22','63','33'
10200 DATA 'B'
10220 DATA 4,7,'NNYYYY','YYYYYY','YYYYYY','12','44','34','24','Y141','00','00','14','Y161','32','53','43'
10230 DATA 0,7,'NYYYYY','NNYYYY','YYYYYY','41','52','32','12','Y441','00','15','00','Y161','32','53','24'
10240 DATA 7,10,'NNYYYY','YYYYYY','NNYYYY','12','23','44','34','Y161','00','53','43','11','32','43','23'
10250 DATA 7,10,'NNYYYY','YYYYYY','NYNYYY','23','44','34','15','Y161','53','00','24','11','32','53','23'
10260 DATA 7,9,'NNYYYY','YYNYYY','NYYYYY','Y441','00','00','00','Y161','32','53','23','Y152','00','33','13'
10270 DATA 7,9,'NNYYYY','YYNYYY','NYYYYY','23','44','34','14','Y161','53','00','23','Y152','12','33','00'
10280 DATA 0,6,'NNYYYY','YYYYYY','NNYYYY','Y441','00','00','16','Y261','33','44','24','11','22','33','44'
10290 DATA 0,8,'NYYYYN','NNYYYY','NNYYYY','41','52','32','22','22','44','34','15','31','42','12','23'
10300 DATA 'C'
10320 DATA 0,8,'NYYYYY','YYYYYY','YYYYYY','21','42','53','00','Y361','45','35','25','Y161','32','53','43'
10330 DATA 0,8,'NYYYYY','YYYYYY','YYYYYY','21','42','53','33','Y361','00','45','25','Y161','32','53','24'
10340 DATA 0,8,'NNYYYY','YYYYYY','YYYYYY','41','21','00','13','Y361','24','45','35','Y161','00','53','43'
10350 DATA 0,8,'NNYYYY','NNYYYY','YYYYYY','Y142','00','33','13','24','45','35','16','Y161','00','53','24'
10360 DATA 0,8,'NYYYYY','NNYYYY','YYNYYY','21','42','32','53','Y541','00','00','00','Y161','32','53','23'
10370 DATA 0,8,'NNYYYY','NNYYYY','YYNYYY','41','21','32','13','24','45','35','15','Y161','00','53','23'
10380 DATA 0,7,'YYYYYY','YYYYYY','NNYYYY','00','42','63','53','Y361','34','45','25','11','22','33','44'
10390 DATA 0,9,'NYYYYN','NNYYYY','NNYYYY','42','53','33','23','23','45','35','16','31','42','12','23'
10400 DATA '[C#/Db]'
10420 DATA 0,7,'NYYYYY','YYYYYY','YYYYYY','Y131','22','43','54','Y461','46','36','26','Y161','32','53','43'
10430 DATA 0,9,'YNYYYN','YYYYYY','YYYYYY','22','43','64','34','Y461','00','46','26','Y161','32','53','24'
10440 DATA 0,9,'NNYYYY','YYYYYY','YYYYYY','31','42','22','00','Y461','25','46','36','Y161','00','53','43'
10450 DATA 5,9,'NYYYYN','NNYYYY','YYYYYY','31','52','42','22','21','42','32','13','Y161','00','53','24'
10460 DATA 0,9,'NYYYYN','NNYYYY','YYNYYY','22','43','33','54','Y641','00','00','00','Y161','32','53','23'
10470 DATA 0,9,'NNYYYY','NNYYYY','YYNYYY','42','22','33','14','25','46','36','16','Y161','00','53','23'
10480 DATA 0,8,'NYYYYY','YYYYYY','NNYYYY','Y131','00','43','54','Y461','35','46','26','11','22','33','44'
10490 DATA 4,10,'NYYYYN','NNYYYY','NNYYYY','43','54','34','24','21','43','33','14','31','42','12','23'
10500 DATA 'D'
10520 DATA 5,10,'NYYYYY','YYYYYY','YYYYYY','32','12','23','00','Y161','43','33','23','Y161','32','53','43'
10530 DATA 0,7,'NYYYYY','YNYYYN','NYYYYY','21','32','12','00','23','44','65','35','Y141','00','12','00'
10540 DATA 5,10,'NYYYYY','YYYYYY','NYYYYY','11','32','00','23','Y161','22','43','33','Y131','00','00','00'
10550 DATA 5,10,'NYYYYY','YYYYYY','NYYYYY','Y121','32','00','00','Y161','22','43','00','Y141','00','00','00'
10560 DATA 7,10,'NYYYYY','NYYYYY','YYNYYY','32','12','00','00','Y131','00','00','00','Y161','32','53','23'
10570 DATA 6,10,'NYYYYY','NYYYYY','YYNYYY','11','32','00','00','21','42','32','12','Y161','00','53','23'
10580 DATA 5,9,'NYYYYY','YYYYYY','NYYYYY','Y231','00','00','00','Y161','32','43','23','11','22','33','44'
10590 DATA 5,11,'YYYYYY','NNYYYY','NNYYYY','21','62','32','00','21','43','33','14','31','42','12','23'
10600 DATA '[D#/Eb]'
10620 DATA 6,8,'NYYYYY','YYYYYY','NNYYYY','Y331','24','45','56','Y161','43','33','23','Y141','00','00','14'
10630 DATA 0,6,'NNYYYY','YNYYYN','YYYYYY','41','22','33','13','24','45','66','36','Y161','00','43','23'
10640 DATA 6,11,'NNYYYY','YYYYYY','NNYYYY','12','33','44','24','Y161','22','43','33','Y131','00','43','00'
10650 DATA 6,11,'NNYYYY','YYYYYY','NNYYYY','41','22','12','33','Y161','22','43','0','Y141','00','00','00'
10660 DATA 4,8,'NYYYYY','NYYYYN','NNYYYY','Y142','00','33','13','21','42','32','53','Y141','00','00','00'
10670 DATA 6,11,'NYYYYY','YYYNYY','NNYYYY','Y152','12','33','00','Y161','22','43','13','Y131','00','43','23'
10680 DATA 6,8,'NYYYYY','YYYYYY','NNYYYY','Y331','00','45','56','Y161','32','43','23','Y141','00','00','13'
10690 DATA 0,6,'YYYYYY','NYYYYN','NNYYYY','Y151','22','63','33','45','56','36','26','21','43','33','14'
10700 DATA 'E'
10720 DATA 4,7,'YYYYYY','YYYYYY','YYYYYY','31','52','42','00','Y131','22','43','54','Y161','43','33','23'
10730 DATA 0,7,'YYYYYY','YNYYYY','YYYYYY','31','52','42','23','42','23','34','14','Y161','00','43','23'
10740 DATA 0,7,'YYYYYY','YNYYYY','YYYYYY','00','52','42','00','13','34','45','25','Y161','22','43','33'
10750 DATA 0,7,'YYYYYY','YNYYYY','YYYYYY','00','52','42','23','42','23','13','34','Y161','22','43','00'
10760 DATA 5,9,'YYYYYY','YYYYYY','YNYYYY','31','52','42','22','21','42','32','53','Y141','00','00','00'
10770 DATA 0,7,'YYYYYY','YYYYYY','YYYNYY','00','52','42','22','Y252','13','34','00','Y161','22','43','13'
10780 DATA 4,7,'YYYYYY','YYYYYY','YYYYYY','41','31','52','00','Y131','00','43','54','Y161','32','43','23'
10790 DATA 6,7,'YNYYYY','YYYYYY','YNYYYY','31','42','12','23','41','52','32','22','21','43','33','14'
10800 DATA 'F'
10820 DATA 0,5,'NNYYYY','YYYYYY','NYYYYY','Y121','32','43','00','Y161','32','53','43','Y131','22','43','54'
10830 DATA 0,0,'NNYYYY','YYYYYY','YYYYYY','Y141','32','00','00','Y161','32','53','24','43','24','35','15'
10840 DATA 0,0,'NNYYYY','YYYYYY','NNYYYY','Y131','00','43','00','Y161','00','53','43','14','35','46','26'
10850 DATA 0,8,'NNYYYY','YYYYYY','YYYYYY','Y141','00','00','00','Y161','00','53','24','Y161','22','43','00'
10860 DATA 0,6,'NNYYYY','NYYYYY','NYYYYN','Y121','32','43','23','Y341','00','35','15','21','42','32','53'
10870 DATA 0,8,'YYNYYY','NYYYYY','YYYNYY','Y161','00','53','23','Y352','14','35','00','Y161','22','43','13'
10880 DATA 5,8,'NYYYYY','NYYYYY','YYYYYY','21','32','53','43','Y131','00','43','54','Y161','32','43','23'
10890 DATA 7,8,'NNYYYY','NYYYYN','NNYYYY','32','43','13','24','41','52','32','22','21','43','33','14'
10900 DATA '[F#/Gb]'
10920 DATA 6,9,'NNYYYY','NNYYYY','YYYYYY','Y221','33','44','00','Y131','22','43','54','Y161','43','33','23'
10930 DATA 0,0,'NNYYYY','YYYYYY','NNYYYY','Y241','33','00','00','Y261','33','54','25','44','25','36','16'
10940 DATA 0,5,'NNYYYY','YYYYYY','NNYYYY','Y231','00','44','00','Y261','00','54','44','11','32','43','23'
10950 DATA 0,5,'NNYYYY','YYYYYY','NYNYYY','Y241','00','00','00','Y261','00','54','25','11','32','53','23'
10960 DATA 0,0,'NNYYYY','YYNYYY','NYYYYY','Y221','33','44','24','Y261','33','54','24','Y452','00','36','16'
10970 DATA 0,0,'NNYYYY','YNYYYY','NYYYYY','Y231','00','44','24','41','62','Y231','00','Y452','15','36','00'
10980 DATA 0,6,'NNYYYY','YNYYYN','NYYYYY','11','22','33','44','62','22','43','33','Y131','00','43','54'
10990 DATA 8,9,'NNYYYY','NYYYYN','NNYYYY','33','44','14','25','41','52','32','22','21','43','33','14'
11000 DATA 'G'
11020 DATA 0,7,'YYYYYY','YYYYYY','NYYYYY','52','63','13','00','Y361','34','55','45','Y131','22','43','54'
11030 DATA 0,5,'YYYYYY','YYYYYY','YYYYYY','11','52','63','00','Y361','34','55','26','41','22','33','13'
11040 DATA 0,6,'NNYYYY','YYYYYY','NNYYYY','Y331','00','45','00','Y361','00','55','45','11','32','43','23'
11050 DATA 0,5,'NNYYYY','YYYYYY','NNYYYY','Y341','00','00','00','Y361','00','55','26','41','22','12','33'
11060 DATA 0,5,'YYYYYY','YYNYYY','XYYYYY','52','63','00','00','Y361','34','55','25','Y152','00','33','13'
11070 DATA 0,5,'NNYYYY','YNYYYY','NYYYYY','Y331','00','45','25','42','63','Y331','00','Y152','12','33','00'
11080 DATA 0,7,'NNYYYY','YNYYYN','NYYYYY','12','23','34','45','63','23','44','34','Y131','00','43','54'
11090 DATA 0,9,'YNYYYY','YYYYYY','NYYYYN','11','32','63','00','34','45','15','26','41','52','32','22'
11100 DATA '[G#/Ab]'
11120 DATA 0,8,'NNYYYY','YYYYYY','NYYYYY','Y141','00','00','14','Y461','35','56','46','Y131','22','43','54'
11130 DATA 4,6,'NNYYYY','YYYYYY','NNYYYY','Y141','00','12','00','Y161','32','53','24','41','22','33','13'
11140 DATA 0,7,'NNYYYY','YYYYYY','NNYYYY','Y431','00','46','00','Y461','00','56','46','11','32','43','23'
11150 DATA 4,7,'NNYYYY','YYYYYY','NYNYYY','Y441','00','00','00','Y161','00','53','24','11','32','53','23'
11160 DATA 0,6,'NNYYYY','YYNYYY','NYYYYY','Y141','00','00','00','Y461','35','56','26','Y152','00','33','13'
11170 DATA 0,6,'NNYYYY','YNYYYY','NYYYYY','Y431','00','46','26','43','64','Y431','00','Y152','12','33','00'
11180 DATA 0,8,'NNYYYY','YNYYYN','NYYYYY','13','24','35','46','64','24','45','35','Y131','00','43','54'
11190 DATA 5,10,'YNYYYY','NNYYYY','NYYYYN','Y142','12','33','64','31','42','12','23','41','52','32','22'
11200 DATA 0,0,'YYYYYY','YNYYYY','YYYYYY','41','21','32','12','Y142','62','32','00','Y261','53','44','24'
11210 DATA 0,0,'NNYYYY','YNYYYN','NYYYYY','42','22','33','13','Y242','63','33','00','51','42','22','00'
11220 DATA 0,0,'NNYYYY','YNYYYN','YYYYYY','43','23','34','14','Y342','64','34','00','Y161','52','43','23'
11300 DATA 0,0,'NNYYYY','YYYYYN','NNYYYY','31','21','42','00','Y132','42','53','64','Y132','42','00','14'
11310 DATA 0,0,'NNYYYY','YYYYYN','NYYYYY','11','32','22','43','Y232','43','54','65','Y232','43','00','15'
11320 DATA 0,0,'NNYYYY','YYYYYN','NNYYYY','12','33','23','44','Y332','44','55','66','Y332','44','00','16'
11330 DATA 0,0,'NNYYYY','YYYYYN','NNYYYY','13','34','24','45','41','52','63','00','41','00','00','13'
11340 :
25140 DEFine PROCedure XTV
25145   REMark SBASIC only - set TV windows to fill screen
25150   OUTLN #0,FLIM_W(#0),FLIM_H(#0),0,0
25160   WINDOW FLIM_W(#0),FLIM_H(#0)-52,0,0 : WINDOW #2,FLIM_W(#0),FLIM_H(#0)-52,0,0 : WINDOW #0,FLIM_W(#0),52,0,FLIM_H(#0)-52
25170   BORDER #1,1,255 : BORDER #2,1,255 : BORDER #0,1,255
25180   CLS : CLS #2 : CLS #0
25190 END DEFine XTV
